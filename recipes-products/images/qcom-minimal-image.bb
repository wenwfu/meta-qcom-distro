SUMMARY = "Minimal image"

LICENSE = "BSD-3-Clause-Clear"

IMAGE_FEATURES += "splash tools-debug allow-root-login post-install-logging"

inherit core-image features_check extrausers image-adbd

# let's make sure we have a good image..
REQUIRED_DISTRO_FEATURES = "pam systemd"

CORE_IMAGE_BASE_INSTALL += " \
    kernel-modules \
    packagegroup-qcom-utilities-bluetooth-utils \
    packagegroup-qcom-utilities-filesystem-utils \
    resize-rootfs \
    zram \
"

# Default root password: oelinux123
EXTRA_USERS_PARAMS = "usermod -p '\$6\$UDMimfYF\$akpHo9mLD4z0vQyKzYxYbsdYxnpUD7B7rHskq1E3zXK8ygxzq719wMxI78i0TIIE0NB1jUToeeFzWXVpBBjR8.' root;"

# Adding kernel-devsrc to provide kernel development support on SDK
TOOLCHAIN_TARGET_TASK += "kernel-devsrc"

# Add RT tools only if the RT kernel is selected
CORE_IMAGE_EXTRA_INSTALL += "${@bb.utils.contains_any('PREFERRED_PROVIDER_virtual/kernel', \
                            'linux-qcom-rt linux-qcom-next-rt', 'rt-tests', '', d)}"

# UKI workaround for targets using multi-dtb (dtb provided by the firmware)
## To be removed once https://lists.openembedded.org/g/openembedded-core/message/231436 is accepted
python __anonymous() {
    qcom_dtb_default = d.getVar("QCOM_DTB_DEFAULT")
    if qcom_dtb_default == "multi-dtb":
        d.setVar("KERNEL_DEVICETREE", "")
}

# On EFI machines the system boots via the ESP partition and UKIs, so the
# kernel/loader files staged in /boot are unused; remove them so /boot is an
# empty mountpoint and systemd can mount the ESP there at runtime.
clean_boot_mountpoint() {
    rm -rf "${IMAGE_ROOTFS}/boot"
    mkdir "${IMAGE_ROOTFS}/boot"
}
ROOTFS_POSTPROCESS_COMMAND += "${@bb.utils.contains('MACHINE_FEATURES', 'efi', 'clean_boot_mountpoint ', '', d)}"

BAD_RECOMMENDATIONS += "systemd-networkd"
